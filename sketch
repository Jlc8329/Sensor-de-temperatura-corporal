#include <WiFi.h>
#include <PubSubClient.h>
#include <DHT.h>

#define DHTPIN 4
#define DHTTYPE DHT22

DHT dht(DHTPIN, DHTTYPE);

const char* ssid = "Wokwi-GUEST";
const char* password = "";

const char* mqtt_server = "test.mosquitto.org";
const int mqtt_port = 1883;

const char* temp_topic = "gustavo10290057/paciente/temperatura";
const char* cmd_topic  = "gustavo10290057/paciente/acao";

WiFiClient espClient;
PubSubClient client(espClient);

int ledPin = 13;
unsigned long lastMsg = 0;

void reconnect() {
  while (!client.connected()) {
    Serial.print("Conectando ao MQTT...");
    String clientId = "ESP32_" + String(random(0xffff), HEX);

    if (client.connect(clientId.c_str())) {
      Serial.println("Conectado!");
      client.subscribe(cmd_topic);
    } else {
      Serial.print("Falhou! rc=");
      Serial.println(client.state());
      delay(1500);
    }
  }
}

void setup() {
  Serial.begin(115200);

  dht.begin();

  WiFi.begin(ssid, password);
  Serial.print("Conectando ao WiFi...");
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print(".");
    delay(300);
  }
  Serial.println("\nWiFi conectado!");

  pinMode(ledPin, OUTPUT);

  client.setServer(mqtt_server, mqtt_port);
}

void loop() {
  if (!client.connected()) reconnect();
  client.loop();

  unsigned long now = millis();
  if (now - lastMsg > 3000) {
    lastMsg = now;

    float temp = dht.readTemperature();

    if (isnan(temp)) {
      Serial.println("Erro ao ler o sensor!");
      return;
    }

    if (temp > 37.5) {
      Serial.print("FEBRE DETECTADA! Temperatura: ");
      Serial.println(temp);
      digitalWrite(ledPin, HIGH);
    } else {
      Serial.print("Temperatura normal: ");
      Serial.println(temp);
      digitalWrite(ledPin, LOW);
    }

    char msg[10];
    dtostrf(temp, 4, 2, msg);
    client.publish(temp_topic, msg);

    Serial.print("Publicado no MQTT: ");
    Serial.println(msg);
    Serial.println("-------------------------");
  }
}
